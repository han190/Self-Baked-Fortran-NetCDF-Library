#: include "module_common.fypp"
submodule(module_interface) submodule_attribute
implicit none
contains

#: for nc_type, type, kind in zip(nc_types, types, kinds)
!> ${type}$(${kind}$) attribute constructor
module pure function new_att_vec_${kind}$(name, vals) result(att)
  character(*), intent(in) :: name
  ${type}$(${kind}$), intent(in) :: vals(:)
  type(attribute_${kind}$_type) :: tmp
  type(attribute_type) :: att

  tmp%len = size(vals)
  tmp%name = name
  tmp%type = ${nc_type}$
  tmp%vals = vals
  att%att = tmp
end function new_att_vec_${kind}$ ${"\n"}$
#: endfor

#: for nc_type, type, kind in zip(nc_types, types, kinds)
!> ${type}$(${kind}$) attribute constructor
module pure function new_att_scal_${kind}$(name, val) result(att)
  character(*), intent(in) :: name
  ${type}$(${kind}$), intent(in) :: val
  type(attribute_${kind}$_type) :: tmp
  type(attribute_type) :: att

  tmp%len = 1
  tmp%name = name
  tmp%type = ${nc_type}$
  tmp%vals = [val]
  att%att = tmp
end function new_att_scal_${kind}$ ${"\n"}$
#: endfor

module pure function new_att_scal_char(name, val) result(att)
  character(len=*), intent(in) :: name, val
  type(attribute_char_type) :: tmp
  type(attribute_type) :: att

  tmp%len = len(val)
  tmp%name = name
  tmp%type = nc_char
  tmp%vals = [val]
  att%att = tmp
end function new_att_scal_char

!> Attributes contructor
module function new_atts(atts) result(ret)
  type(attribute_type), intent(in) :: atts(:)
  type(attribute_type), allocatable :: ret(:)
  integer :: i

  ret = atts
  do i = 1, size(ret)
    ret(i)%att%ID = i - 1
  end do
end function new_atts

!> Put attribute (internal)
subroutine put_att_(ncid, varid, att)
  integer(c_int), intent(in) :: ncid, varid
  type(attribute_type) :: att
  integer(c_int) :: stat

  select type (att_ => att%att)
  #: for c_name, kind in zip(c_names[1:], kinds[1:])
  type is (attribute_${kind}$_type)

    stat = nc_put_att_${c_name}$( &
      & ncid, varid, att_%name, &
      & att_%type, att_%len, att_%vals)
    call handle_error(stat, "nc_put_att_${c_name}$")

  #: endfor
  type is (attribute_char_type)

    stat = nc_put_att_text( &
      & ncid, varid, att_%name, &
      & len(att_%vals(1), c_size_t), att_%vals(1))
    call handle_error(stat, "nc_put_att_text")

  end select 
end subroutine put_att_

!> Put attributes (internal)
subroutine put_atts_(ncid, varid, atts)
  integer(c_int), intent(in) :: ncid, varid
  type(attribute_type), intent(in) :: atts(:)
  integer :: i

  do i = 1, size(atts)
    call put_att_(ncid, varid, atts(i))
  end do
end subroutine put_atts_

!> Put group attributes
module subroutine put_grp_atts(grp)
  class(group_type), intent(in) :: grp

  if (allocated(grp%atts)) &
    & call put_atts_(grp%ID, nc_global, grp%atts)
end subroutine put_grp_atts

!> Put variable attributes
module subroutine put_var_atts(var)
  type(variable_type), intent(in) :: var

  associate (var_ => var%var)
    if (allocated(var_%atts)) &
      & call put_atts_(var_%grpID, var_%ID, var_%atts)
  end associate
end subroutine put_var_atts

end submodule submodule_attribute