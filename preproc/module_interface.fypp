#: include "module_common.fypp"
module module_interface

use :: iso_fortran_env
use :: iso_c_binding
use :: module_c_interface
implicit none

public :: attribute_type, dimension_type, variable_type
public :: operator(.dim.), operator(.att.)
public :: dims, atts, data_array, to_netcdf
public :: write(formatted), shape
private

!> NetCDF Data Model
!> -----------------

!> The data model follows the netCDF data model introduced
!> https://docs.unidata.ucar.edu/netcdf-c/current/netcdf_data_model.html

!> Currently supported data types
!> - [x] char
!> - [ ] byte
!> - [x] short
!> - [x] int
!> - [x] int64
!> - [x] float
!> - [x] double
!> - [ ] unsigned byte
!> - [ ] unsigned short
!> - [ ] unsigned int
!> - [ ] unsigned int64
!> - [ ] string

!> Abstract NetCDF type
type, abstract :: netcdf_type
  integer(c_int) :: ID
  character(len=:), allocatable :: name
end type netcdf_type

!> Dimension type
type, extends(netcdf_type) :: dimension_type
  integer(c_size_t) :: len = 0
  logical :: is_unlim = .false.
end type dimension_type

!> Attribute type
type, abstract, extends(netcdf_type) :: abstract_attribute_type
  integer(c_size_t) :: len = 0
  integer(c_int) :: type = 0
end type abstract_attribute_type

#: for type, kind in zip(types, kinds)
!> ${type}$(${kind}$) attribute type
type, extends(abstract_attribute_type) :: attribute_${kind}$_type
  ${type}$ (${kind}$), allocatable :: vals(:)
end type attribute_${kind}$_type ${"\n"}$
#: endfor

!> Character attribute type
type, extends(abstract_attribute_type) :: attribute_char_type
  character(:), allocatable :: vals(:)
end type attribute_char_type

!> Attributes_type
type :: attribute_type
  class(abstract_attribute_type), allocatable :: att
end type attribute_type

!> Abstract variable type
type, abstract, extends(netcdf_type) :: abstract_variable_type
  type(dimension_type), allocatable :: dims(:)
  type(attribute_type), allocatable :: atts(:)
  integer(c_int), pointer  :: grpID => null()
  integer(c_int) :: type = 0
end type abstract_variable_type

#: for type, kind in zip(types, kinds)
!> ${type}$(${kind}$) attribute type
type, extends(abstract_variable_type) :: variable_${kind}$_type
  ${type}$ (${kind}$), allocatable :: vals(:)
end type variable_${kind}$_type ${"\n"}$
#: endfor

!> Character variable type
type, extends(abstract_variable_type) :: variable_char_type
  character(:), allocatable :: vals(:)
end type variable_char_type

!> Variable type
type :: variable_type
  class(abstract_variable_type), allocatable :: var
end type variable_type

!> Group type
type, extends(netcdf_type) :: group_type
  type(dimension_type), allocatable :: dims(:)
  type(attribute_type), allocatable :: atts(:)
  type(variable_type), allocatable :: vars(:)
  type(group_type), allocatable :: grps(:)
  integer(c_int) :: mode = 0
  integer(c_int) :: fmt = 0
end type group_type

!> File type (root group)
type, extends(group_type) :: file_type
  character(len=:), allocatable :: filename
end type file_type

!> Dimension constructor
interface operator(.dim.)
  module procedure :: new_dim
end interface operator(.dim.)

!> Dimensions constructor
interface dims
  module procedure :: new_dims
  module procedure :: new_dims_unlim
end interface dims

!> Attribute constructor
interface operator(.att.)
  #: for kind in kinds + ["char"]
  module procedure :: new_att_scal_${kind}$
  #: endfor
  #: for kind in kinds
  module procedure :: new_att_vec_${kind}$
  #: endfor
end interface operator(.att.)

!> Attributes constructor
interface atts
  module procedure :: new_atts
end interface atts

!> Variable constructor
interface data_array
  #: for rank in range(num_ranks)
  #: set cols = (":," + "".join([":," for _ in range(rank)]))[:-1]
  #: for type, kind in zip(types, kinds)
  module procedure :: new_variable_${kind}$_${rank + 1}$d
  module procedure :: new_variable_noatt_${kind}$_${rank + 1}$d
  #: endfor
  #: endfor
  #: del cols
end interface data_array

!> to NetCDF
interface to_netcdf
  module procedure :: to_netcdf_var
end interface to_netcdf

!> IO
interface write(formatted)
  module procedure :: write_formatted_dim
  module procedure :: write_formatted_att
  module procedure :: write_formatted_var
end interface write(formatted)

!> Shape
interface shape
  module procedure :: shape_dims
  module procedure :: shape_var
end interface shape

!> Interfaces to submodules
interface
  !> submodule utility
  !> -----------------

  module pure function cstr(string) result(cstring)
    character(len=*), intent(in) :: string
    character(kind=c_char, len=:), allocatable :: cstring
  end function cstr

  module pure function cstrip(cstring) result(string)
    character(len=*), intent(in) :: cstring
    character(:), allocatable :: string
  end function cstrip

  module subroutine handle_error(stat, err_msg)
    integer(c_int), intent(in) :: stat
    character(*), intent(in), optional :: err_msg
  end subroutine handle_error

  !> submodule dimension
  !> -------------------

  module pure function new_dim(name, len) result(dim)
    character(len=*), intent(in) :: name
    integer, intent(in) :: len
    type(dimension_type) :: dim
  end function new_dim

  module function new_dims(dims) result(ret)
    type(dimension_type), intent(in) :: dims(:)
    type(dimension_type), allocatable :: ret(:)
  end function new_dims

  module function new_dims_unlim(dims, unlim_dim) result(ret)
    type(dimension_type), intent(in) :: dims(:)
    integer, intent(in) :: unlim_dim
    type(dimension_type), allocatable :: ret(:)
  end function new_dims_unlim

  module pure function shape_dims(dims) result(ret)
    type(dimension_type), intent(in) :: dims(:)
    integer, allocatable :: ret(:)
  end function shape_dims

  module subroutine def_grp_dim(grp)
    class(group_type), intent(in) :: grp
  end subroutine def_grp_dim

  module subroutine def_var_dim(var)
    type(variable_type), intent(in) :: var
  end subroutine def_var_dim

  !> submodule attribute
  !> -------------------

  #: for nc_type, type, kind in zip(nc_types, types, kinds)
  module pure function new_att_vec_${kind}$(name, vals) result(att)
    character(*), intent(in) :: name
    ${type}$(${kind}$), intent(in) :: vals(:)
    type(attribute_type) :: att
  end function new_att_vec_${kind}$ ${"\n"}$
  #: endfor

  #: for nc_type, type, kind in zip(nc_types, types, kinds)
  module pure function new_att_scal_${kind}$(name, val) result(att)
    character(*), intent(in) :: name
    ${type}$(${kind}$), intent(in) :: val
    type(attribute_type) :: att
  end function new_att_scal_${kind}$ ${"\n"}$
  #: endfor
  
  module pure function new_att_scal_char(name, val) result(att)
    character(len=*), intent(in) :: name, val
    type(attribute_type) :: att
  end function new_att_scal_char

  module function new_atts(atts) result(ret)
    type(attribute_type), intent(in) :: atts(:)
    type(attribute_type), allocatable :: ret(:)
  end function new_atts

  module subroutine put_grp_atts(grp)
    class(group_type), intent(in) :: grp
  end subroutine put_grp_atts

  module subroutine put_var_atts(var)
    type(variable_type), intent(in) :: var
  end subroutine put_var_atts

  !> submodule variable
  !> ------------------

  #: for rank in range(num_ranks)
  #: set cols = (":," + "".join([":," for _ in range(rank)]))[:-1]
  #: for type, kind in zip(types, kinds)
  module function new_variable_${kind}$_${rank + 1}$d( &
    & data, name, dims, atts) result(var)
    ${type}$(${kind}$), target, contiguous, intent(in) :: data(${cols}$)
    character(len=*), intent(in) :: name
    type(dimension_type), intent(in) :: dims(:)
    type(attribute_type), intent(in) :: atts(:)
    type(variable_type) :: var
  end function new_variable_${kind}$_${rank + 1}$d ${"\n"}$

  module function new_variable_noatt_${kind}$_${rank + 1}$d( &
    & data, name, dims) result(var)
    ${type}$(${kind}$), target, contiguous, intent(in) :: data(${cols}$)
    character(len=*), intent(in) :: name
    type(dimension_type), intent(in) :: dims(:)
    type(variable_type) :: var
  end function new_variable_noatt_${kind}$_${rank + 1}$d ${"\n"}$
  #: endfor
  #: endfor
  #: del cols

  module pure function shape_var(var) result(ret)
    type(variable_type), intent(in) :: var
    integer, allocatable :: ret(:)
  end function shape_var

  module subroutine to_netcdf_var(var, filename)
    type(variable_type), intent(inout) :: var
    character(len=*), intent(in) :: filename
  end subroutine to_netcdf_var

  !> submodule I/O
  !> -------------

  module subroutine write_formatted_dim( &
    & dim, unit, iotype, v_list, iostat, iomsg)
    class(dimension_type), intent(in) :: dim
    integer, intent(in) :: unit
    character(*), intent(in) :: iotype
    integer, intent(in) :: v_list(:)
    integer, intent(out) :: iostat
    character(*), intent(inout) :: iomsg
  end subroutine write_formatted_dim

  module subroutine write_formatted_att( &
    & att, unit, iotype, v_list, iostat, iomsg)
    class(attribute_type), intent(in) :: att
    integer, intent(in) :: unit
    character(*), intent(in) :: iotype
    integer, intent(in) :: v_list(:)
    integer, intent(out) :: iostat
    character(*), intent(inout) :: iomsg
  end subroutine write_formatted_att

  module subroutine write_formatted_var( &
    & var, unit, iotype, v_list, iostat, iomsg)
    class(variable_type), intent(in) :: var
    integer, intent(in) :: unit
    character(*), intent(in) :: iotype
    integer, intent(in) :: v_list(:)
    integer, intent(out) :: iostat
    character(*), intent(inout) :: iomsg
  end subroutine write_formatted_var
end interface

end module module_interface
